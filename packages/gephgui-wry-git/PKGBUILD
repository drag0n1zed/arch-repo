# Maintainer: drag <drag0n1zed@outlook.com>
pkgname=gephgui-wry-git
_pkgname=gephgui-wry
pkgver=r0.0
pkgrel=1
pkgdesc="Geph connects you with the censorship-free Internet (GUI Client)"
arch=('x86_64')
url="https://github.com/geph-official/gephgui-wry"
depends=(
    'webkit2gtk-4.1'
    'libappindicator-gtk3'
    'gtk3'
)
makedepends=(
  'npm'
  'git'
  'cargo'
  'sqlite'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/geph-official/gephgui-wry.git")
sha512sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    # Generate `pkgver`
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$_pkgname"
    # Make sure the `gephgui` submodule is here
    git submodule update --init --recursive

    # Download Rust dependencies
    mkdir -p target
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd "$_pkgname"

    cd gephgui
    npm install -f
    npm run build
    cd ..

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release
}

package () {
    cd "$_pkgname"

    # Move binary
    install -d "$pkgdir/usr/lib/gephgui-wry"
    install -Dm755 "target/release/gephgui-wry" "$pkgdir/usr/lib/gephgui-wry/gephgui-wry"

    # Make wrapper script (fixes rendering for wayland for me)
    install -d "$pkgdir/usr/bin"
    cat > "$pkgdir/usr/bin/gephgui-wry" << EOF
#!/bin/sh
export WEBKIT_DISABLE_COMPOSITING_MODE=1
exec /usr/lib/gephgui-wry/gephgui-wry "\$@"
EOF
    chmod 755 "$pkgdir/usr/bin/gephgui-wry"

    # Install pac binary (what is this used for?)
    install -Dm755 "blobs/linux-x64/pac-real" "$pkgdir/usr/bin/pac"

    # Install icon
    install -Dm644 "flatpak/icons/256x256/io.geph.GephGui.png" \ "$pkgdir/usr/share/icons/hicolor/256x256/apps/geph.png"

    # Install Desktop file
    mkdir -p "$pkgdir/usr/share/applications"
    cat > "$pkgdir/usr/share/applications/geph.desktop" << EOF
[Desktop Entry]
Type=Application
Name=Geph
Comment=Privacy and censorship circumvention tool
Exec=/usr/bin/gephgui-wry
Icon=geph
Terminal=false
Categories=Network;
EOF
    chmod 644 "$pkgdir/usr/share/applications/geph.desktop"
}